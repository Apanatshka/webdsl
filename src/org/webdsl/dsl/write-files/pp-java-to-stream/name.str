module org/webdsl/dsl/write-files/pp-java-to-stream/name

imports
  libstratego-lib
  libjava-front

rules

  pkgname-to-stream(s) =
    ?PackageName(name)
  ; <print-id-list(s)> name

  print-id-list(s) =
    ?[ Id(x) | xs ]
  ; <s> x
  ; <map({y: ?Id(y); !"."; s; !y; s })> xs

rules

  typename-to-stream(s) =
    ?TypeName(Id(x))
  ; <s> x

  typename-to-stream(s) =
    ?TypeName(name, Id(x))
  ; <not(?PackageName([]))> name
  ; <pkgname-to-stream(s) <+ ptname-to-stream(s)> name
  ; <s> "."
  ; <s> x

  typename-to-stream(s) =
    ?TypeName(PackageName([]), Id(x))
  ; <s> x

rules

  ptname-to-stream(s) =
    ?PackageOrTypeName(Id(x))
  ; <s> x

  ptname-to-stream(s) =
    ?PackageOrTypeName(name, Id(x))
  ; <ptname-to-stream(s)> name
  ; <s> "."
  ; <s> x

  ptname-to-stream(s) =
    ?PackageOrTypeName(PackageName([]), Id(x))
  ; <s> x

rules

  methodname-to-stream(s) =
    ?MethodName(Id(x))
  ; <s> x

  methodname-to-stream(s) =
    ?MethodName(q, Id(x))
  ; <ambname-to-stream(s)> q
  ; <s> "."
  ; <s> x

rules

  lval-to-stream(s) = ambname-to-stream(s)

  typename-to-stream(s) = ambname-to-stream(s)

  ambname-to-stream(s) =
    ?AmbName(Id(x))
  ; <s> x

  ambname-to-stream(s) =
    ?AmbName(q, Id(x))
  ; <ambname-to-stream(s) <+ pkgname-to-stream(s)> q
  ; <s> "."
  ; <s> x

rules

  expr-to-stream(s) = lval-to-stream(s)

  lval-to-stream(s) =
    ?ExprName(Id(x))
  ; <s> x

  lval-to-stream(s) =
    ?ExprName(q, Id(x))
  ; <lval-to-stream(s)> q
  ; <s> "."
  ; <s> x

  lval-to-stream(s) =
    ?Field(Id(x))
  ; <s> x
