module org/webdsl/dsl/write-files/pp-java-to-stream/literal

imports
  libstratego-lib
  libjava-front

rules

  lit-inner-to-stream(s) =
    ?Null()
  ; <s> "null"

  lit-inner-to-stream(s) =
    ?VoidClass()
  ; <s> "void.class"

  lit-inner-to-stream(s) =
    ?Class(type)
  ; <classorinterfacetype-to-stream(s)> type
  ; <s> ".class"

  lit-inner-to-stream(s) =
    ?Bool(b)
  ; <bool-to-stream(s)> b

  bool-to-stream(s) =
    ?True()
  ; <s> "true"

  bool-to-stream(s) =
    ?False()
  ; <s> "false"

  lit-inner-to-stream(s) =
    ?Char(c)
  ; <s> "'"
  ; <s> c
  ; <s> "'"

  lit-inner-to-stream(s) =
    ?Single(c)
  ; <implode-string; s> [c]

rules

  escape-to-stream(s) =
    ?NamedEscape(c)
  ; <implode-string> ['\', c]
  ; s

  escape-to-stream(s) =
    ?UnicodeEscape(us, c1, c2, c3, c4)
  ; <implode-string> ['\' | <conc> (us, [c1, c2, c3, c4])]
  ; s

  escape-to-stream(s) =
    ?OctaEscape1(c1)
  ; <implode-string> ['\', c1]
  ; s

  escape-to-stream(s) =
    ?OctaEscape2(c1, c2)
  ; <implode-string> ['\', c1, c2]
  ; s

  escape-to-stream(s) =
    ?OctaEscape3(c1, c2, c3)
  ; <implode-string> ['\', c1, c2, c3]
  ; s

rules

  lit-inner-to-stream(s) =
    ?String(parts)
  ; <s> "\""
  ; <map(chars-to-stream(s) <+ escape-to-stream(s))> parts
  ; <s> "\""

  chars-to-stream(s) =
    ?Chars(c)
  ; <s> c

rules

  lit-inner-to-stream(s) =
    ?Float(f)
  ; <s> f

  lit-inner-to-stream(s) =
    ?Deci(d)
  ; <s> d

  lit-inner-to-stream(s) =
    ?Hexa(h)
  ; <s> h

  lit-inner-to-stream(s) =
    ?Octa(o)
  ; <s> o
