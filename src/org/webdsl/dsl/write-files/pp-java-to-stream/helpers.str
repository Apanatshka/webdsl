module org/webdsl/dsl/write-files/pp-java-to-stream/helpers

imports
  libstratego-lib
  libjava-front

rules

  //stmt-to-stream(s) = debug(|"stmt"); fail
  //expr-to-stream(s) = debug(|"expr"); fail

  block-structure-stream(s,t) =
    ?(1, elems)
  ; not(!elems => [])
  ; <s> "{\n"
  ; <map(t)> elems
  ; <s> "}\n"

  block-structure-stream(s,t) =
    ?(0, elems)
  ; not(!elems => [])
  ; <s> "{\n"
  ; <map(t)> elems
  ; <s> "}\n"

  block-structure-stream(s,t) =
    ?(_, [])
  ; <s> "{}\n"

rules

  list-to-args-stream(s) =
    ?[]
  ; <s> "()"

  list-to-args-stream(s) =
    ?exprs@[ _ | _ ]
  ; <s> "("
  ; <print-comma-sep(s,expr-to-stream(s))> exprs
  ; <s> ")"

rules

  array-to-args-stream(s) =
    ?[]
  ; <s> "{}"

  array-to-args-stream(s) =
    ?exprs@[ _ | _ ]
  ; <s> "{"
  ; <print-comma-sep(s,expr-to-stream(s))> exprs
  ; <s> "}"

rules

  if-some(s) = ?None() <+ ?Some(<s>)

rules

  print-comma-sep(s,t) =
    ?[ x | xs ]
  ; <t> x
  ; <map({y: ?y; !","; s; !y; t })> xs

  print-comma-sep(s,t) =
    ?[]
