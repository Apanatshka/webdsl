module org/webdsl/dsl/write-files/pp-body-to-stream/class/method-declaration

imports
  libstratego-lib
  libjava-front

rules

  body-to-stream(s) =
    ?MethodDec(dec, b@Block(_))
  ; <methodechead-to-stream(s)> dec
  ; <block-to-stream(s)> b

  body-to-stream(s) =
    ?MethodDec(dec, NoMethodBody())
  ; <methodechead-to-stream(s)> dec
  ; <s> ";\n"

rules

  methodechead-to-stream(s) =
    ?MethodDecHead(mods, type-params, type, Id(n), params, throws)
  ; <map(mod-to-stream(s))> mods
  ; <if-some(typeparams-to-stream(s))> type-params
  ; <type-to-stream(s)> type
  ; <s> " "
  ; <s> n
  ; <params-to-stream(s)> params
  ; <throws-to-stream(s)> throws
/*
  methodechead-to-stream(s) =
    ?DeprMethodDecHead(mods, type-params, type, Id(n), params, dims, throws)
*/
rules
  params-to-stream(s) =
    ?[]
  ; <s> "()"

  params-to-stream(s) =
    ?list@[ x | xs ]
  ; <s> "("
  ; <print-comma-sep(s,param-to-stream(s))> list
  ; <s> ")"

// Param([],"java.util.List<webdsl.generated.domain.SubmissionProxy>",Id("subsNoKey__"))

  param-to-stream(s) =
    ?Param(mods, type, vardecid)
  ; <map(mod-to-stream(s))> mods
  ; <type-to-stream(s) <+ is-string; s> type // "org.webdsl.foo" string where type constructor is expected indicates codegen issue
  ; <s> " "
  ; <?Id(<s>)> vardecid

  param-to-stream(s) =
    ?VarArityParam(mods, type, vardecid)
  ; <map(mod-to-stream(s))> mods
  ; <type-to-stream(s)> type
  ; <s> "..."
  ; <?Id(<s>)> vardecid

rules

  throws-to-stream(s) =
    ?Some(ThrowsDec(exceptions))
  ; <s> "throws "
  ; <print-comma-sep(s,classtype-to-stream(s))> exceptions

  throws-to-stream(s) =
    ?None()

rules

  type-to-stream(s) =
    ?Void()
  ; <s> "void"
