module org/webdsl/dsl/languages/action/for-exp

imports
  libwebdsl-front
  org/webdsl/dsl/typechecker/rename
  org/webdsl/dsl/typechecker/utils
  org/webdsl/dsl/typechecker/error
  org/webdsl/dsl/languages/composition
  org/webdsl/dsl/transformation/emit
  org/webdsl/dsl/languages/action/not-null-checks

signature
  constructors
    ClosureInAnno : List(Exp) -> Annotation

rules

  rename-action :
    fe@ForExp(oe1, x, srt, ie1, f1, gb1){anno*} -> ForExp(oe2, y, srt2, ie2, f2, gb2){anno1*}
    with  anno1* := <put-closure-in-anno> anno*
        ; ie2 := <rename-all> ie1
        ; {| Rename, VarInContext
          : if <?None()> srt
            then GenericSort(_,[srt2]) := <type-of> ie2 
                 <+ srt2 := "unknown"
            else srt2 := <rename-all> srt end
          ; y := <rename-bound(|srt2)> x
          ; rules(VarDecl : y -> fe)
          ; <declare-vars-in-entity-context> y
          ; oe2 := <rename-all> oe1
          ; f2 := <rename-all> f1
          ; gb2 := <rename-all> gb1
          |}

  rename-action :
    AndForExp(e1){anno*} -> AndForExp(e2){anno1*}
    with  anno1* := <put-closure-in-anno> anno*
        ; e2 := <rename-all> e1

  rename-action :
    OrForExp(e1){anno*} -> OrForExp(e2){anno1*}
    with  anno1* := <put-closure-in-anno> anno*
        ; e2 := <rename-all> e1

rules

  put-closure-in-anno :
    anno* -> anno1*
    with  // collect variables for for expression closure, similar to elements/with templatecall ui/templatecall.str
          oldvars := <fetch-elem(?ClosureInAnno(<id>)) <+ ![]> anno*
        ; if InDef <+ InEntity //includes template/page defines, global/entity functions 
          then vars := <bigbagof-VarInContext> //whole def renamed, collect environment
          else vars := <rename-all> oldvars //only this part renamed, only apply rename
          end
        ; anno1* := [ClosureInAnno(vars)|anno*]
        
  get-closure-from-anno :
    anno* -> params
    with  params := <fetch-elem(?ClosureInAnno(<id>)) <+ ![]> anno*
          //if not renamed/no closure in annotation, assume nothing in scope/empty closure (<+ ![])
          
  get-args-fargs-from-closure-in-anno-filtered : //only add what is actually used
    (anno*,code) -> (argfiltered*,farg*)
    with  arg* := <get-closure-from-anno> anno*
        ; argfiltered* := <filter(var-is-used-in(|code))> arg*
        ; farg* := <map(lifting-param-to-farg)> argfiltered*
   
  var-is-used-in(|code):
    a@Var(x){anno*} -> Var(x){anno*}
    where <collect(?a{anno*})> code
        ; not(?[])
    
  get-args-fargs-from-closure-in-anno : //get the entire closure
    anno* -> (arg*,farg*)
    with  arg* := <get-closure-from-anno> anno*
        ; farg* := <map(lifting-param-to-farg)> arg*
        
  lifting-param-to-farg :
    Var(x) -> farg|[ x : srt ]|
    with  srt := <type-of> Var(x)
        
rules

  eq-type-of-action :
    AndForExp(fe) -> SimpleSort("Bool")
    where GenericSort(_, [SimpleSort("Bool")]) := <type-of> fe

  eq-type-of-action :
    OrForExp(fe) -> SimpleSort("Bool")
    where GenericSort(_, [SimpleSort("Bool")]) := <type-of> fe

  eq-type-of-action :
    ForExp(oe, x, s, ie, _, gb) -> t
    where toe := <type-of> oe //type added by rename rule above
        ; if <?None()> gb
          then t := GenericSort("List", [toe])
          else t := GenericSort("List", [GenericSort("List", [toe])]) end

rules

  constraint-error-action :
    AndForExp(fe) -> <add-error(|["And-for-expression should contain a collection of Bool type"])>
    where not(SimpleSort("Bool") := <type-of>)

  constraint-error-action :
    OrForExp(fe) -> <add-error(|["Or-for-expression should contain a collection of Bool type"])>
    where not(SimpleSort("Bool") := <type-of>)

  constraint-error-action : ForExp(_, _, srt, col, _, _)      -> <forexp-constraint-error(|srt,col)>

  forexp-constraint-error(|srt,col) :
    fe -> <add-error(|["collection in for-expression does not contain declared type ",<pp-type> srt])>
    where not(GenericSort(<?"Set"<+?"List">, [srt]) := <type-of> col)

rules

  desugar-action :
    fe@ForExp(e_mapto, x, s, e_collection, fltr, gb){anno*} -> <rename> ThisCall(x_fun, arg*)
    with  x_fun := <toplevel-rename> "listCompr"
        ; (arg*,farg*) := <get-args-fargs-from-closure-in-anno-filtered> (anno*,fe)
        ; farg2* := <map(\ Arg(x, srt{srtanno*}){anno*} -> Arg(x, srt{<remove-all(?IsPropertyType())>srtanno*}){anno*} \)> farg*
        ; srt_collection := <type-of> e_collection
        ; GenericSort(_, [srt]) := srt_collection
        ; srt_mapto := <type-of> e_mapto
        ; if <?FieldAccess(Var("this"),_)> e_collection
          then e_nullcheck := True() 
          else e_nullcheck := <add-not-null-checks> NotEq(Var("thecollection"),Null())
          end
        ; stat_colvar := <add-immutable-anno> stat|[ var thecollection := e_collection; ]| // The immutable anno on the vardecl is used by the query analysis, so `thecollection` becomes a synonym for `e_collection`
        ; if <?None()> gb
          then srt_result := GenericSort("List", [srt_mapto])
             ; e_emptyresultlist := |[ List<srt_mapto>() ]|
             ; stat_add* :=  stat*|[ res.add(e_mapto); ]|
             ; stat_init* := []
          else srt_result := GenericSort("List",[GenericSort("List", [srt_mapto])])
             ; e_emptyresultlist := |[ List<List<srt_mapto>>() ]|
             ; e_groupby := <?GroupBy(<id>)> gb
             ; srt_groupby := <type-of> e_groupby
             ; stat_init* := stat*|[ var groups: [srt_groupby] := List<srt_groupby>(); ]|
             ; stat_add* :=  stat*|[ var name := e_groupby;
                                     if( !(name in groups) ){
                                       groups.add( name );
                                       res.add( List<srt_mapto>() );
                                     }
                                     res[ groups.indexOf( name ) ].add( e_mapto ); ]|
          end
        ; def := def|[
            function x_fun (farg2*): srt_result {
              var res : srt_result;
              res := e_emptyresultlist;
              stat_colvar
              stat_init*
              if( e_nullcheck ){
                for( x : srt in thecollection fltr ){
                  stat_add*
                }
              } else {}
              return res;
            }
          ]|
        ; <rename-this-var;emit-webdsl-code> def{anno*}

  keep-position-anno = ?ForExp(_, _, _, _, _, _)

  desugar-action :
    AndForExp(e){anno*} -> <rename> ThisCall(x_fun, arg*)
    with  x := <newname-webdsl> "x"
        ; x_fun := <newname-webdsl> "andForExp"
        ; (arg*,farg*) := <get-args-fargs-from-closure-in-anno-filtered> (anno*,e)
        ; def := def|[
            function x_fun (farg*) : Bool {
              for(x : Bool in e) {
                if(!x) {
                  return false;
                } 
              }
              return true;
            }
          ]|
        ; <rename-this-var;emit-webdsl-code> def{anno*}

  keep-position-anno = ?AndForExp(_)  

  desugar-action :
    OrForExp(e){anno*} -> <rename> ThisCall(x_fun, arg*)
    with  x := <newname-webdsl> "x"
        ; x_fun := <newname-webdsl> "orForExp"
        ; (arg*,farg*) := <get-args-fargs-from-closure-in-anno-filtered> (anno*,e)
        ; def := def|[
            function x_fun (farg*) : Bool {
              for(x : Bool in e) {
                if(x) {
                  return true;
                } 
              }
              return false;
            }
          ]|
        ; <rename-this-var;emit-webdsl-code> def{anno*}

  keep-position-anno = ?OrForExp(_)  

rules

  /**
   *  'this' is normally not renamed, but in this case it should, because 'this' as farg name is not allowed
   */
  rename-this-var =
    try(
      where( 
        varname := <toplevel-rename> "renamedthis"
      )
    ; alltd(\Var("this"{anno*}) -> Var(varname)\)
    ; alltd(\Arg("this"{anno*},srt) -> Arg(varname,srt)\)
    )
    