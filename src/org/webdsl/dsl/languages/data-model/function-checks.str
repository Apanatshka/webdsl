module org/webdsl/dsl/languages/data-model/function-checks

imports
  libstrategolib

imports
  libwebdsl-front
  org/webdsl/dsl/languages/data-model/property-checks

rules 
  
 constraint-error-global-nondouble-entities =
      ?x_class
    ; bagof-Functions
    ; constraint-double-decls(
          extract-function-sig; sig-to-lower
        ,{prettysig:  
            Snd; extract-function-sig; pp-func-sig => prettysig
          ; <concat-strings> ["Function with signature ",prettysig," of entity '",x_class,"' is defined multiple times."]
         }
      )
           
rules // generic check entity function traversal

  constraint-error-data =
      where(get-entity-def <+ get-extend-entity-def)
    ; transform-entity-body(check-entity-functions)
  
rules // extend and cached function
  
  check-entity-functions(|entityname):
    decls -> <fail>
    with <filter(check-entity-function(|entityname))> decls
       ; sigs := <filter(is-function-cons; extract-function-sig)> decls
       ; efuncs := <filter(?ExtendFunction(_))> decls
       ; <filter(match-extend-function-to-entity-function(|entityname))> efuncs
       ; cachedFuns := <filter(has-anno(?SimpleAnno("cache")))> decls
       ; <filter(is-cachable-function)> cachedFuns
         
  match-extend-function-to-entity-function(|entityname):
    ExtendFunction(f) -> <add-error(|["Extend function with signature ",prettysig," in ",entityname," extends a non-existing function."])>
    where sig@(name,argtypes) := <extract-function-sig> f
        ; <not(CheckFunctionSignature)> (SimpleSort(<capitalize-string> entityname),name,argtypes)
        ; <not(CheckFunctionSignature)> (SimpleSort("Entity"),name,argtypes)
        ; prettysig := <pp-func-sig> sig 
          
  is-cachable-function():
  	f@Function(_, fargs, _, _) -> <fail>
  	where <not(?[])> fargs
  	    ; <add-error(|["Only functions without arguments can be cached. Remove the cached keyword, or changed the function to not have arguments."])> f
  is-cachable-function():
  	f@Function(_, _, srt, _) -> <fail>
  	where <(?SimpleSort("Void"))> srt
  	    ; <add-error(|["Only functions that have a return type can be cached. Remove the cached keyword, or changed the function to return a result."])> f 
     
rules // built-in function signature check
  
  check-entity-function(|entityname):
    decl -> <fail>
    where is-function-cons
      ; sig := <extract-function-sig> decl
      ; msg := <is-builtin-ent-func-signature(|entityname)> sig
      ; prettysig := <pp-func-sig> sig
      ; <add-error(|
              ["Function with signature ", prettysig, " for entity ", entityname,
               " ", msg ] )> decl

rules
  
  is-builtin-ent-func-signature(|entname):
    (name, params) -> msg
    where fargs := <EntityFunctionNoOverloading> (entname, name)
        ; not(params := fargs)
        ; msg := <concat-strings> ["overloads a builtin function."]
  