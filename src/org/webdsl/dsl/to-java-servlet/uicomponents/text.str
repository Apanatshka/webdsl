module org/webdsl/dsl/to-java-servlet/uicomponents/text

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front
 
rules // text

  element-to-java-servlet = (?Text(_) <+ ?TextInterp(_) <+ ?TemplateCall("text",[_],_,_)); element-render(|<render-phase-text>)

  render-phase-text :
    Text(val) ->
    bstm*|[ bstm_output ]|
    with  bstm_output := <to-page-string-java-servlet-skip-try> String(<string-replace(|"\\~","~")> val)

  render-phase-text :
    TextInterp(val) ->
    bstm*|[ bstm_output ]|
    with  bstm_output := <to-page-string-java-servlet-skip-try> StringInterp(val)

  // convert an expression to text (string value of the expression)
  render-phase-text :
    TemplateCall("text", [arg],passign*, TemplateBody([])) -> 
    bstm*|[ bstm_output ]|
    with  bstm_output := <to-page-string-java-servlet> arg
