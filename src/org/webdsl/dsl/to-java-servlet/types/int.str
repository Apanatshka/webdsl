module org/webdsl/dsl/to-java-servlet/types/int

imports
  libstrategolib
  libjava-front
  libwebdsl-front

rules // java type

  builtin-java-type :
    SimpleSort("Int") -> type|[ Integer ]|

rules // Int literal

   expression-to-java-servlet :
     Int(x) -> Lit(Deci(x))

rules // conversion

  call-to-java-servlet :
    exp|[ e.floatValue() ]| -> java|[ new Integer(e1).floatValue() ]|
    where SimpleSort("Int") := <get-type-anno> e
    with  e1    := <expression-to-java-servlet> e

  call-to-java-servlet :
    exp|[ random(e1, e2) ]| -> java|[ java.util.concurrent.ThreadLocalRandom.current().nextInt(e1', e2') ]|
    with  e1' := <expression-to-java-servlet> e1
       ;  e2' := <expression-to-java-servlet> e2

  call-to-java-servlet :
    exp|[ random(e) ]| -> java|[ java.util.concurrent.ThreadLocalRandom.current().nextInt(e') ]|
    with  e' := <expression-to-java-servlet> e
